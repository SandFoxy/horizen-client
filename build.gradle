plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "me.chrommob.skidfuscatorgradle"
}

apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenLocal()
    mavenCentral()

    maven { url = "https://maven.fabricmc.net/" }
    maven { url = "https://branchlock.net/storage/maven/" }
}
loom {
    runs {
        client {
            client()
            setConfigName("Minecraft Client")
            ideConfigGenerated(true)
            runDir("run")
            vmArg("-Dsodium.checks.issue2561=false")
        }
    }
}
dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation "io.github.spair:imgui-java-binding:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-binding:${project.imguiVersion}"
    implementation "io.github.spair:imgui-java-lwjgl3:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-lwjgl3:${project.imguiVersion}"

    implementation "io.github.spair:imgui-java-natives-windows:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-natives-windows:${project.imguiVersion}"
    implementation "io.github.spair:imgui-java-natives-linux:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-natives-linux:${project.imguiVersion}"
    implementation "io.github.spair:imgui-java-natives-macos:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-natives-macos:${project.imguiVersion}"

    //Neuro Killaura
    implementation 'com.google.code.gson:gson:2.10.1'
    shadow 'com.google.code.gson:gson:2.10.1'

    //Audio Player
//    implementation 'dev.redstones.mediaplayerinfo:MediaPlayerInfo:0.1.0'
//    shadow 'dev.redstones.mediaplayerinfo:MediaPlayerInfo:0.1.0'

    //WebSocket
    implementation 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.18'
    implementation 'javax.websocket:javax.websocket-api:1.1'
    shadow 'javax.websocket:javax.websocket-api:1.1'
    shadow 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.18'
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.mod_version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "fabric_version": project.fabric_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
//    classifier 'shadow-dev'
    dependencies {
        exclude(dependency('org.lwjgl:lwjgl'))
        exclude(dependency('org.lwjgl:lwjgl-glfw'))
        exclude(dependency('org.lwjgl:lwjgl-opengl'))
    }
}


remapJar {
    dependsOn shadowJar
    input.set shadowJar.archiveFile.get()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar.archivePath) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
//        maven {
//            credentials {
//                username "$mavenUser"
//                password "$mavenPassword"
//            }
//            name = 'breadloaf.public'
//            url = 'https://maven.breadloaf.xyz/repository/public'
//        }

    }
}